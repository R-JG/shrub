|%
++  run
  |=  txt=@t
  (scan (trip txt) apex:rein)
+$  dep
  [face=(unit term) =pith]
+$  file
  $:  deps=(list dep)
      =hoon
  ==
++  rein
  |%
  ++  dep
    ^-  $-(nail (like ^dep))
    ;~  pose
      (rune hep (stag ~ stip))
      (rune lus ;~(plug (stag ~ sym) ;~(pfix gap stip)))
    ==
  ++  rune
    |*  [car=rule rul=rule]
    (ifix [;~(plug fas car gap) gay] rul)

  ++  deps
    (star dep)
  ++  hone
    =+  vaz=vast
    (ifix [gay gay] tall:vaz)
  ++  apex
    ^-  $-(nail (like file))
    :: ^-  rule
    ;~(plug deps hone)
  --
--

